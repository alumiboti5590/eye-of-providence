plugins {
  id 'java-library'
  id 'edu.wpi.first.GradleRIO' version "${wpilibVersion}"
  id 'com.diffplug.spotless' version '6.+'
  id 'signing'
  id 'maven-publish'
  id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = 'com.alumiboti5590'
version = getTagName();

def getTagName() {
    String ref = System.getenv('GITHUB_REF') ?: "0.0.1-SNAPSHOT";
    return ref.split('/').last();
}

java {
  withSourcesJar()
  withJavadocJar()
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = "sources"
}

repositories {
  maven {
    url "https://maven.revrobotics.com/"
  }
  maven {
    url "https://maven.ctr-electronics.com/release/"
  }
}


// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 5.
dependencies {
  // Core WPILib dependencies
  implementation wpi.java.deps.wpilib()
  implementation wpi.java.vendor.java()
  // New command structure dependency is not pulled by default
  implementation "edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:${wpilibVersion}"

  // RevRobotics dependency (Spark Max, Neos, etc...)
  implementation "com.revrobotics.frc:REVLib-java:${revRoboticsVersion}"

  // CTRE dependency (Talon SRX, etc...)
  implementation "com.ctre.phoenix:api-java:${ctreVersion}"
  implementation "com.ctre.phoenix:wpiapi-java:${ctreVersion}"

  // Other WPILib dependencies
  nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
  nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
  simulationDebug wpi.sim.enableDebug()

  nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
  nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
  simulationRelease wpi.sim.enableRelease()

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
  useJUnitPlatform()
  testLogging {
    exceptionFormat "full" // default is "short"
    events "passed", "skipped", "failed"
  }

  systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
  options.compilerArgs.add '-XDstringConcat=inline'
}

// Configure the linting and style-enforcement of the project
spotless {
  format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }

  java {
    importOrder()
    removeUnusedImports()

    palantirJavaFormat()

    // make sure every file has the following copyright header.
    // optionally, Spotless can set copyright years by digging
    // through git history (see "license" section below)
    licenseHeader '/* $YEAR Written by Alumiboti FRC 5590 */'
  }
}

publishing {
  // Configure our package for when it gets published
  publications {
    maven(MavenPublication) {
      artifactId = 'eye-of-providence'
      from components.java

      pom {
        name = 'Eye of Providence'
        description = 'Collection of reusable code for the FRC robots maintained by the Alumiboti.'
        url = 'https://github.com/alumiboti5590/eye-of-providence'

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://github.com/alumiboti5590/eye-of-providence/blob/main/LICENSE.md'
          }
        }

        developers {
          developer {
            id = 'dstarner'
            name = 'Dan Starner'
            email = 'alumiboti@danstarner.com'
          }
        }
      }
    }
  }
}

signing {
    def key = System.getenv("SIGNING_KEY")
    def password = System.getenv("SIGNING_PASSWORD")

    useInMemoryPgpKeys(key, password)
    sign publishing.publications.maven
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      username = "dstarner"
      password = "c8Sm00/btYZWvlwtvI6CdY8jFwTiaLcjRsZXB/hzqhgA"
    }
  }
}
